/// # base

/// ## base

/// ### emit
inl emit forall t. (x : t) : t =
    $'!x '

/// ### emit_unit
inl emit_unit forall t. (x : t) : () =
    $'!x '

/// ### use
inl use forall t. (x : t) : t =
    $'use !x = !x ' : ()
    $'!x '

/// ### unit
nominal unit = $'unit'

/// ### eval
inl eval fn =
    fn ()

/// ### flip
inl flip fn a b =
    fn b a

/// ### do
inl do (body : () -> ()) : () =
    !!!!Do (body())

/// ### let'
inl let' fn =
    inl result : unit =
        backend_switch {
            Gleam = fun () => $'Nil' : unit
            Lua = fun () => $'nil' : unit
            Fsharp = fun () => $'()' : unit
            Python = fun () => $'None' : unit
        }
    backend_switch {
        Gleam = fun () =>
            $'let _let\'_!result = {' : ()
            fn |> indent
            $'}' : ()
        Lua = fun () =>
            $'local _get_let_!result = function()' : ()
            fn |> indent
            $'end' : ()
            $'local _let_!result = _get_let_!result()' : ()
        Fsharp = fun () =>
            $'let _let\'_!result =' : ()
            fn |> indent
        Python = fun () =>
            $'def _let\'_!result():' : ()
            fn |> indent
    }
    $'_let\'_!result '

/// ### exec_unit
inl exec_unit (fn : () -> ()) : () =
    backend_switch {
        Gleam = fun () => fn ()
        Lua = fun () => fn ()
        Fsharp = fun () =>
            inl unit = $'()' : $'unit'
            ($'(fun () -> !fn (); !unit) ()' : $'unit') |> ignore
        Python = fun () => fn ()
    }

/// ### lazy
nominal lazy t = $'Lazy<`t>'

/// ### memoize
inl memoize forall t. (fn : () -> t) : () -> t =
    inl body () =
        inl result = mut None
        inl computed = mut false
        fun () =>
            if *computed
            then *result
            else
                result <- fn () |> Some
                computed <- true
                *result
            |> optionm.value
        : () -> t
    backend_switch {
        Gleam = fun () => body ()
        Lua = fun () => body ()
        Fsharp = fun () =>
            inl result : lazy t = $'lazy !fn ()'
            fun () => $'!result.Value' : t
        Python = fun () => body ()
    }

/// ### capture
inl capture forall t. (fn : () -> t) : t =
    backend_switch {
        Gleam = fun () =>
            fn ()
        Lua = fun () =>
            fn ()
        Fsharp = fun () =>
            inl result = dyn true
            $'let mutable _capture_!result : `t option = None '
            $'('
            $'(fun () ->'
            $'(fun () ->'
            fn () |> emit_unit
            $')'
            $'|> fun x -> x ()'
            $') () )'
            $'|> fun x -> _capture_!result <- Some x'
            $'match _capture_!result with Some x -> x | None -> failwith "base.capture / _capture_!result=None"' : t
        Python = fun () =>
            fn ()
    }

/// ### yield_from
inl yield_from forall (t : * -> *) u. (a : t u) : () =
    backend_switch {
        Fsharp = fun () => $'yield\! !a ' : ()
        Python = fun () => $'asyncio.run(!a())' : ()
    }

/// ### join_body
inl join_body body acc x =
    if var_is x |> not
    then body acc x
    else
        inl acc = dyn acc
        join body acc x

/// ### join_body_unit
inl join_body_unit body d x =
    if var_is d |> not
    then body x
    else
        inl x = dyn x
        join body x

/// ### (+.)
inl (+.) forall t. (a : t) (b : t) : t =
    $'!a + !b '

/// ### (-.)
inl (-.) forall t. (a : t) (b : t) : t =
    $'!a - !b '

/// ### (*.)
inl (*.) forall t. (a : t) (b : t) : t =
    $'!a * !b '

/// ### (/.)
inl (/.) forall t. (a : t) (b : t) : t =
    $'!a / !b '

/// ### (=.)
inl (=.) forall t. (a : t) (b : t) : bool =
    backend_switch {
        Gleam = fun () => $'!a == !b ' : bool
        Lua = fun () => $'!a == !b ' : bool
        Fsharp = fun () => $'!a = !b ' : bool
        Python = fun () => $'!a == !b ' : bool
    }

/// ### (<>.)
inl (<>.) forall t. (a : t) (b : t) : bool =
    backend_switch {
        Gleam = fun () => $'!a \!= !b ' : bool
        Lua = fun () => $'!a ~= !b ' : bool
        Fsharp = fun () => $'!a <> !b ' : bool
        Python = fun () => $'!a \!= !b ' : bool
    }

/// ### (<>..)
inl (<>..) a b =
    fun () => a = b
    |> dyn
    |> eval
    |> not

/// ### append
prototype append t : t -> t -> t

/// ### (++)
inl (++) a b =
    b |> append a

/// ### pair
type pair_switch a b =
    {
        Gleam : $'\#(`a, `b  )'
        Lua : $'(`a, `b)'
        Fsharp : $'(`a * `b)'
        Python : $'(`a, `b)'
    }
nominal pair a b = $'backend_switch `(pair_switch a b)'

inl pair x y =
    x, y

/// ### new_pair
inl new_pair forall a b. (a : a) (b : b) : pair a b =
    backend_switch {
        Gleam = fun () =>
            $'\#(!a, !b )' : pair a b
        Lua = fun () =>
            $'{!a, !b}' : pair a b
        Fsharp = fun () =>
            $'!a, !b ' : pair a b
        Python = fun () =>
            $'!a, !b ' : pair a b
    }

/// ### from_pair
inl from_pair forall a b. (pair : pair a b) : a * b =
    backend_switch {
        Gleam = fun () =>
            $'let \#(a, b) = !pair '
            ($'a' : a), ($'b' : b)
        Lua = fun () =>
            $'local a, b = !pair[1], !pair[2]'
            ($'a' : a), ($'b' : b)
        Fsharp = fun () =>
            $'let (a, b) = !pair '
            ($'a' : a), ($'b' : b)
        Python = fun () =>
            $'a, b = !pair '
            ($'a' : a), ($'b' : b)
    }

/// ### (||>)
inl (||>) (arg1, arg2) fn =
    arg2 |> fn arg1

/// ### (||>)

/// ### fix_condition
inl fix_condition x a b =
    if x ()
    then a () |> fun x => $'(* fix_condition then' : ()
    else
        $'fix_condition then *) else' : ()
        b () |> fun x => $'(* fix_condition else' : ()
    |> fun x => $'fix_condition else *)' : ()

/// ### infer
nominal infer = $'_'

/// ### infer'
nominal infer' t = $'_'

/// ### any
nominal any = $'obj'

/// ### null
inl null forall t. () : t =
    backend_switch {
        Gleam = fun () => $'Nil' : t
        Lua = fun () => $'nil' : t
        Fsharp = fun () => $'null |> unbox<`t>' : t
        Python = fun () => $'None' : t
    }

/// ### defaultof
inl defaultof forall t. () : t =
    inl run default =
        real
            typecase t with
            | string => ""
            | int => 0
            | char => '\0'
            | option ~u => None `u
            | _ => default ()
    backend_switch {
        Gleam = fun () => run (fun () => (real failwith `t "base.defaultof / invalid type") : t) : t
        Lua = fun () => run (fun () => (real failwith `t "base.defaultof / invalid type") : t) : t
        Fsharp = fun () => run (fun () => $'Unchecked.defaultof<`t>' : t) : t
        Python = fun () => run (fun () => (real failwith `t "base.defaultof / invalid type") : t) : t
    }

/// ### choice2'
nominal choice2' a b = $'Choice<`a, `b>'

/// ### choice2_unbox
inl choice2_unbox forall t1 t2. (choice : choice2' t1 t2) : choice2 t1 t2 =
    run_target_args (fun () => choice) function
        | Fsharp _ => fun choice =>
            inl c1of2 (x : t1) : _ _ t2 = C1of2 x
            inl c2of2 (x : t2) : _ t1 _ = C2of2 x
            inl c1of2 = join c1of2
            inl c2of2 = join c2of2
            $'match !choice with Choice1Of2 x -> !c1of2 x | Choice2Of2 x -> !c2of2 x'
        | _ => fun _ => null ()

/// ### ref
nominal ref t = $'`t ref'

/// ### new_ref
inl new_ref forall t. (x : t) : ref t =
    $'ref !x '

/// ### ref_value
inl ref_value forall t. (x : ref t) : t =
    $'!x.Value'

/// ### ref_set_value
inl ref_set_value forall t. (value : t) (ref : ref t) : ref t =
    $'!ref.Value <- !value '
    ref

/// ### exn
type exn_switch =
    {
        Gleam : $'Nil'
        Lua : $'nil'
        Fsharp : $'exn'
        Python : $'BaseException'
    }
nominal exn = $'backend_switch `(exn_switch)'

inl exn x =
    x |> $'`exn '

/// ### try
inl try forall t. (fn : () -> t) (ex_fn : exn -> option t) : option t =
    inl some x : option t = Some x
    inl some = dyn some
    inl fn = dyn fn
    inl ex_fn = dyn ex_fn
    backend_switch {
        Gleam = fun () =>
            fn () |> some
        Lua = fun () =>
            fn () |> some
        Fsharp = fun () =>
            $'let result = ref !(None : option t)'
            $'try'
            $'    result.Value <- !fn () |> !some '
            $'with ex ->'
            $'    result.Value <- !ex_fn ex '
            $'result.Value' : option t
        Python = fun () =>
            $'result = !(None : option t)'
            inl fn = dyn fn
            inl ex_fn = dyn ex_fn
            $'try:'
            $'    result = !some(!fn())\n        \'\'\''
            $'\'\'\''
            $'except Exception as e:'
            $'    result = !ex_fn(e)'
            $'result' : option t
    }

/// ### try_unit
inl try_unit forall t. (fn : () -> ()) (ex_fn : (() -> exn) -> ()) : t =
    backend_switch {
        Gleam = fun () => $'// base.try_unit' : ()
        Lua = fun () => $'-- base.try_unit' : ()
        Fsharp = fun () => $'try' : ()
        Python = fun () => $'try:' : ()
    }
    fn |> indent
    backend_switch {
        Gleam = fun () => $'// base.try_unit' : ()
        Lua = fun () => $'-- base.try_unit' : ()
        Fsharp = fun () => $'with ex ->' : ()
        Python = fun () => $'except Exception as ex:' : ()
    }
    fun () =>
        inl ex = $'ex'
        backend_switch {
            Fsharp = fun () => $'()' : ()
        }
        inl ex () =
            ex
        ex_fn ex
    |> indent
    backend_switch {
        Gleam = fun () => $'// base.try_unit' : ()
        Lua = fun () => $'-- base.try_unit' : ()
        Fsharp = fun () =>
            $'|> ignore'
            $'(* try_unit'
            $'try_unit *)' : t
        Python = fun () => $'None' : t
    }

/// ### try_unit'
inl try_unit' forall t. (ex_fn : (() -> exn) -> ()) (fn : () -> ()) : t =
    try_unit fn ex_fn

/// ### try_finally
inl try_finally forall t. (fn : () -> ()) (finally : () -> ()) : t =
    backend_switch {
        Gleam = fun () => $'// base.try_finally' : ()
        Lua = fun () => $'-- base.try_finally' : ()
        Fsharp = fun () => $'try' : ()
        Python = fun () => $'try:' : ()
    }
    fn |> indent
    backend_switch {
        Gleam = fun () => $'// base.try_finally' : ()
        Lua = fun () => $'-- base.try_finally' : ()
        Fsharp = fun () => $'finally' : ()
        Python = fun () => $'finally:' : ()
    }
    finally |> indent
    backend_switch {
        Gleam = fun () => $'// base.try_finally' : ()
        Lua = fun () => $'-- base.try_finally' : ()
        Fsharp = fun () =>
            $'(* try_finally'
            $'try_finally *)'
            ()
        Python = fun () => $'// base.try_finally' : ()
    }

/// ### (:>)
prototype (~:>) r : forall t. t -> r
