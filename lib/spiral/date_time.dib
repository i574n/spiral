#!meta

{"kernelInfo":{"defaultKernelName":"spiral","items":[{"name":"spiral"}]}}

#!markdown

# date_time

#!spiral

///- --test static

open testing

#!spiral

open rust.rust_operators
open sm'_operators

#!markdown

## date_time

#!markdown

### timestamp

#!spiral

nominal timestamp_gleam =
    `(
        backend_switch `(()) `({}) {
            Gleam = (fun () => global "import gleam/time/timestamp") : () -> ()
        }
        $'' : $'timestamp.Timestamp'
    )
nominal timestamp_python =
    `(
        backend_switch `(()) `({}) {
            Python = (fun () => global "import datetime") : () -> ()
        }
        $'' : i64
    )
type timestamp_switch =
    {
        Gleam : timestamp_gleam
        Fsharp : i64
        Python : timestamp_python
    }
nominal timestamp = $'backend_switch `(timestamp_switch)'

inl timestamp forall t {int}. (x : t) : timestamp =
    run_target function
        | Gleam => fun () =>
            backend_switch {
                Gleam = fun () => global "import gleam/time/timestamp"
            }
            x |> $'timestamp.from_unix_seconds'
        | _ => fun () => x |> convert.unbox

#!markdown

### timestamp_guid

#!spiral

type timestamp_guid = guid.guid

#!markdown

### date_time_guid

#!spiral

type date_time_guid = guid.guid

#!markdown

### test_guid

#!spiral

///- --test static

inl test_guid () =
    "6543210F-EDCB-A987-6543-210FEDCBA987" |> guid.new_guid

#!markdown

## day_of_week

#!spiral

union day_of_week =
    | Sunday
    | Monday
    | Tuesday
    | Wednesday
    | Thursday
    | Friday
    | Saturday

#!markdown

## month

#!spiral

union month =
    | January
    | February
    | March
    | April
    | May
    | June
    | July
    | August
    | September
    | October
    | November
    | December

#!markdown

## day

#!spiral

nominal day = int

#!markdown

## year

#!spiral

nominal year = int

#!markdown

## fsharp

#!markdown

### date_time

#!spiral

nominal date_gleam =
    `(
        backend_switch `(()) `({}) {
            Gleam = (fun () =>
                global "import gleam/time/calendar"
                        ) : () -> ()
        }
        $'' : $'calendar.Date'
    )
nominal time_of_day_gleam =
    `(
        backend_switch `(()) `({}) {
            Gleam = (fun () =>
                global "import gleam/time/calendar"
                        ) : () -> ()
        }
        $'' : $'calendar.TimeOfDay'
    )
nominal duration_gleam =
    `(
        backend_switch `(()) `({}) {
            Gleam = (fun () =>
                global "import gleam/time/duration"
                        ) : () -> ()
        }
        $'' : $'duration.Duration'
    )

nominal date_time_python =
    `(
        backend_switch `(()) `({}) {
            Python = (fun () => global "import datetime") : () -> ()
        }
        $'' : $'datetime.datetime'
    )
type date_time_switch =
    {
        Gleam : date_gleam * time_of_day_gleam * duration_gleam
        Fsharp : $'System.DateTime'
        Python : date_time_python
    }
nominal date_time = $'backend_switch `(date_time_switch)'

#!markdown

### calendar_month

#!spiral

nominal calendar_month =
    `(
        backend_switch `(()) `({}) {
            Gleam = (fun () =>
                global "import gleam/time/calendar"
                        ) : () -> ()
        }
        $'' : $'calendar.Month'
    )

#!markdown

### tempo_date_time

#!spiral

nominal tempo_date_time =
    `(
        backend_switch `(()) `({}) {
            Gleam = (fun () =>
                global "import tempo"
                        ) : () -> ()
        }
        $'' : $'tempo.DateTime'
    )

#!markdown

### year

#!spiral

inl get_year (date_time : date_time) : i32 =
    backend_switch {
        Gleam = fun () => $'0' : i32
        Lua = fun () => $'0' : i32
        Fsharp = fun () => date_time |> $'_.Year' : i32
        Python = fun () => $'!date_time.year' : i32
    }

#!markdown

### split_date_time

#!spiral

inl split_date_time (date_time : date_time) : date_gleam * time_of_day_gleam * duration_gleam =
    $'!date_time.0', $'!date_time.1', $'!date_time.2'

#!markdown

### new_timestamp_gleam

#!spiral

inl new_timestamp_gleam
    (date, time_of_day, duration : date_gleam * time_of_day_gleam * duration_gleam)
    : timestamp
    =
    global "import gleam/time/timestamp"
    inl result : timestamp = $'timestamp.from_calendar(!date, !time_of_day, !duration)'

    global "import tempo/datetime"
    inl tempo_date_time : tempo_date_time =
        result |> $'datetime.from_timestamp'
    inl tempo_date_time : tempo_date_time = $'!tempo_date_time |> datetime.add(!duration)'
    tempo_date_time |> $'datetime.to_timestamp'

#!markdown

### format

#!spiral

inl format (format : string) (date_time : date_time) : string =
    backend_switch {
        Gleam = fun () =>
            global "import tempo/datetime"
            inl tempo_date_time : tempo_date_time =
                date_time |> split_date_time |> new_timestamp_gleam |> $'datetime.from_timestamp'
            // inl tempo_date_time : tempo_date_time = $'!tempo_date_time |> datetime.add(!duration)'
            inl format =
                if format = ""
                then "M-D-YY hh:mm:ss A"
                else format
            $'!tempo_date_time |> datetime.format(tempo.Custom(!format))' : string
            // #?{ date_time format date time_of_day duration timestamp tempo_date_time }
        Fsharp = fun () =>
            inl format =
                if format = ""
                then "M-d-y hh:mm:ss tt"
                else format
            $'!date_time.ToString' format : string
        Python = fun () =>
            inl date_time = join date_time
            if format <> ""
            then $'!date_time.strftime(!format)' : string
            elif get_year date_time < 1000
            then $'\'{dt.month}-{dt.day}-{dt.year} {dt:%I}:{dt:%M}:{dt:%S} {dt:%p}\'.format(dt=!date_time)' : string
            else $'\'{dt.month}-{dt.day}-{dt:%y} {dt:%I}:{dt:%M}:{dt:%S} {dt:%p}\'.format(dt=!date_time)' : string
        Lua = fun () =>
            inl date_time = join date_time
            if format <> ""
            then $'os.date(!format, !date_time)' : string
            else
                inl date =
                    if get_year date_time < 1000
                    then $'(os.date("%m-%d-%y", !date_time) or ""):gsub("0(%d)", "%1")' : string
                    else $'(os.date("%m-%d-%Y", !date_time) or ""):gsub("0(%d)", "%1")' : string
                inl time =
                    $'os.date("%I:%M:%S %p", !date_time) or ""' : string
                date ++# " " ++# time
    }

#!markdown

### format_iso8601

#!spiral

inl format_iso8601 (date_time : date_time) : string =
    backend_switch {
        Gleam = fun () => date_time |> format "YYYY-MM-DDTHH-mm-ss.SSS" : string
        Lua = fun () => date_time |> format "%Y-%m-%dT%H-%M-%S.%f" : string
        Fsharp = fun () => date_time |> format "yyyy-MM-ddTHH-mm-ss.fff" : string
        Python = fun () => date_time |> format "%Y-%m-%dT%H-%M-%S.%f" : string
    }

#!markdown

### min_value

#!spiral

inl min_value () : date_time =
    backend_switch {
        Lua = fun () =>
            inl date_time = $'os.time{year=1, month=1, day=1, hour=0, min=0, sec=0} or -62135596800' : date_time
            date_time
        Fsharp = fun () => $'System.DateTime.MinValue' : date_time
        Python = fun () => $'datetime.datetime.min' : date_time
        Gleam = fun () =>
            inl date : date_gleam = $'calendar.Date(1, calendar.January, 1)' : date_gleam
            inl time_of_day : time_of_day_gleam = $'calendar.TimeOfDay(0, 0, 0, 0)' : time_of_day_gleam
            inl duration : duration_gleam = $'duration.seconds(0) ' : duration_gleam
            (date, time_of_day, duration) |> convert.to : date_time
    }

#!spiral

///- --test
///> fsharp
///> cuda

min_value ()
|> format ""
|> _assert_eq "1-1-1 12:00:00 AM"

#!markdown

### max_value

#!spiral

inl max_value () : date_time =
    backend_switch {
        Gleam = fun () =>
            inl date : date_gleam = $'calendar.Date(9999, calendar.December, 31)' : date_gleam
            inl time_of_day : time_of_day_gleam = $'calendar.TimeOfDay(23, 59, 59, 999)' : time_of_day_gleam
            inl duration : duration_gleam = $'duration.seconds(0)  ' : duration_gleam
            (date, time_of_day, duration) |> convert.to : date_time
        Lua = fun () =>
            inl date_time = $'os.time{year=9999, month=12, day=31, hour=23, min=59, sec=59} or 253402300799' : date_time
            date_time
        Fsharp = fun () => $'System.DateTime.MaxValue' : date_time
        Python = fun () => $'datetime.datetime.max' : date_time
    }

#!spiral

///- --test
///> fsharp
///> cuda

max_value ()
|> format ""
|> _assert_eq "12-31-99 11:59:59 PM"

#!markdown

### to_calendar_utc

#!spiral

inl to_calendar_utc (timestamp : timestamp) : date_time =
    backend_switch {
        Gleam = fun () =>
            global "import gleam/time/calendar"
            global "import gleam/time/timestamp"
    }
    inl duration = $'calendar.utc_offset' : duration_gleam
    inl (date : date_gleam), (time_of_day : time_of_day_gleam) = $'!timestamp |> timestamp.to_calendar(!duration)'
    (date, time_of_day, duration) |> convert.to

#!markdown

### tempo_date_time

#!spiral

nominal time_zone_provider =
    `(
        backend_switch `(()) `({}) {
            Gleam = (fun () =>
                global "import tempo"
                        ) : () -> ()
        }
        $'' : $'tempo.TimeZoneProvider'
    )

#!markdown

### unix_epoch

#!spiral

inl unix_epoch () : date_time =
    backend_switch {
        Gleam = fun () =>
            0i32 |> timestamp |> to_calendar_utc : date_time
        Lua = fun () =>
            inl date_time = $'os.time{year=1970, month=1, day=1, hour=0, min=0, sec=0} or 0' : date_time
            date_time
        Fsharp = fun () => $'System.DateTime.UnixEpoch' : date_time
        Python = fun () => $'datetime.datetime(1970, 1, 1)' : date_time
    }

#!spiral

///- --test
///> lua
///> fsharp
///> gleam
///> cuda

unix_epoch ()
|> format ""
|> _assert_eq "1-1-70 12:00:00 AM"

#!markdown

### date_time_milliseconds

#!spiral

inl date_time_milliseconds
    (year : int) (month : int) (day : int) (hour : int) (minute : int) (second : int) (millisecond : int)
    : date_time
    =
    backend_switch {
        Gleam = fun () =>
            global "import gleam/time/calendar"
            global "import gleam/time/duration"
            global "import gleam/time/timestamp"
            global "import tempo/month"
            inl calendar_month : resultm.result' calendar_month () =
                $"!month |> month.from_int"
            inl calendar_month = calendar_month |> resultm.unbox |> resultm.get
            // TODO: types
            // inl month =
            //     match month with
            //     | 1 => January
            //     | 2 => February
            //     | 3 => March
            //     | 4 => April
            //     | 5 => May
            //     | 6 => June
            //     | 7 => July
            //     | 8 => August
            //     | 9 => September
            //     | 10 => October
            //     | 11 => November
            //     | 12 => December
            // inl month : infer =
            //     match month with
            //     | January => $'calendar.January'
            //     | February => $'calendar.February'
            //     | March => $'calendar.March'
            //     | April => $'calendar.April'
            //     | May => $'calendar.May'
            //     | June => $'calendar.June'
            //     | July => $'calendar.July'
            //     | August => $'calendar.August'
            //     | September => $'calendar.September'
            //     | October => $'calendar.October'
            //     | November => $'calendar.November'
            //     | December => $'calendar.December'
            inl date : date_gleam = $'calendar.Date(#year, #calendar_month, #day)'
            inl nanoseconds = millisecond * 1000000
            inl time_of_day : time_of_day_gleam = $'calendar.TimeOfDay(#hour, #minute, #second, #nanoseconds)'
            inl duration : duration_gleam = $'duration.milliseconds(#millisecond)'
            (date, time_of_day, duration) |> convert.to : date_time
        Lua = fun () =>
            inl total_seconds = second + (minute * 60) + (hour * 3600)
            inl date_time = $'os.time{year=!year, month=!month, day=!day, hour=0, min=0, sec=0} or 0' : int
            date_time + (total_seconds + (millisecond / 1000)) |> convert.unbox : date_time
        Fsharp = fun () =>
            $'System.DateTime (!year, !month, !day, !hour, !minute, !second, !millisecond)' : date_time
        Python = fun () =>
            $'datetime.datetime(!year, !month, !day, !hour, !minute, !second, !millisecond)' : date_time
    }

#!spiral

///- --test
///> gleam
///> lua
///> fsharp
///> cuda

date_time_milliseconds 1970 1 1 0 0 0 0
|> format ""
|> _assert_eq "1-1-70 12:00:00 AM"

#!markdown

### date_time_utc

#!spiral

inl date_time_utc
    (year : int) (month : int) (day : int) (hour : int) (minute : int) (second : int)
    : date_time
    =
    backend_switch {
        Gleam = fun () =>
            global "import gleam/time/calendar"
            global "import gleam/time/duration"
            global "import gleam/time/timestamp"
            global "import tempo/month"
            inl calendar_month : resultm.result' calendar_month () =
                $"!month |> month.from_int"
            inl calendar_month = calendar_month |> resultm.unbox |> resultm.get
            inl date : date_gleam = $'calendar.Date(#year, #calendar_month, #day)'
            inl time_of_day : time_of_day_gleam = $'calendar.TimeOfDay(#hour, #minute, #second, 0)'
            inl duration : duration_gleam = $'duration.Duration(0)   ' : duration_gleam
            (date, time_of_day, duration) |> convert.to : date_time
        Lua = fun () =>
            inl total_seconds = second + (minute * 60) + (hour * 3600)
            inl date_time = $'os.time{year=!year, month=!month, day=!day, hour=0, min=0, sec=0} or 0' : int
            date_time + total_seconds |> convert.unbox : date_time
        Fsharp = fun () =>
            $'System.DateTime (!year, !month, !day, !hour, !minute, !second, System.DateTimeKind.Utc)' : date_time
        Python = fun () =>
            $'datetime.datetime(!year, !month, !day, !hour, !minute, !second, tzinfo=datetime.timezone.utc)' : date_time
    }

#!spiral

///- --test
///> lua
///> fsharp
///> cuda

date_time_utc 1970 1 1 0 0 0
|> format ""
|> _assert_eq "1-1-70 12:00:00 AM"

#!markdown

### date_time_kind

#!spiral

union date_time_kind =
    | Unspecified
    | Utc
    | Local

#!markdown

### specify_date_kind

#!spiral

inl specify_date_kind (kind : date_time_kind) (date_time : date_time) : date_time =
    backend_switch {
        Gleam = fun () => date_time
        Lua = fun () => date_time
        Fsharp = fun () =>
            inl kind : $'System.DateTimeKind' =
                match kind with
                | Unspecified => $'System.DateTimeKind.Unspecified'
                | Utc => $'System.DateTimeKind.Utc'
                | Local => $'System.DateTimeKind.Local'
            $'System.DateTime.SpecifyKind (!date_time, !kind)' : date_time
        Python = fun () => $'!date_time.replace(tzinfo=None)' : date_time
    }

#!markdown

### to_universal_time

#!spiral

inl to_universal_time (date_time : date_time) : date_time =
    backend_switch {
        Gleam = fun () => date_time
        Lua = fun () => date_time
        Fsharp = fun () => date_time |> $'_.ToUniversalTime()' : date_time
        Python = fun () => $'!date_time.replace(tzinfo=datetime.timezone.utc)' : date_time
    }

#!spiral

///- --test
///> lua
///> fsharp
///> cuda

date_time_milliseconds 1970 1 1 0 0 0 0
|> specify_date_kind Utc
|> to_universal_time
|> format ""
|> _assert_eq "1-1-70 12:00:00 AM"

#!spiral

///- --test
///> lua
///> fsharp
///> cuda

date_time_utc 1970 1 1 0 0 0
|> specify_date_kind Utc
|> format ""
|> _assert_eq "1-1-70 12:00:00 AM"

#!spiral

///- --test
///> lua
///> fsharp
///> cuda

date_time_utc 1970 1 1 0 0 0
|> specify_date_kind Local
|> format ""
|> _assert_eq "1-1-70 12:00:00 AM"

#!spiral

///- --test
///> lua
///> fsharp
///> cuda

date_time_utc 1970 1 1 0 0 0
|> specify_date_kind Unspecified
|> format ""
|> _assert_eq "1-1-70 12:00:00 AM"

#!markdown

### time_span

#!spiral

nominal time_span_python =
    `(
        backend_switch `(()) `({}) {
            Python = (fun () => global "import datetime") : () -> ()
        }
        $'' : $'datetime.timedelta'
    )
type time_span_switch =
    {
        Gleam : $'duration.Duration'
        Lua : $'number'
        Fsharp : $'System.TimeSpan'
        Python : time_span_python
    }
nominal time_span = $'backend_switch `(time_span_switch)'

inl time_span x : time_span =
    backend_switch {
        Gleam = fun () => x |> convert.unbox : time_span
        Lua = fun () => x |> convert.unbox : time_span
        Fsharp = fun () => $'!x |> `time_span ' : time_span
        Python = fun () => $'datetime.timedelta(!x)' : time_span
    }

#!markdown

### new_time_span

#!spiral

inl new_time_span (a : date_time) (b : date_time) : time_span =
    // | _ => fun () => $'!b - !a '
    run_target function
        | Gleam => fun () =>
            inl timestamp : timestamp = a |> split_date_time |> new_timestamp_gleam
            inl timestamp' : timestamp = b |> split_date_time |> new_timestamp_gleam
            inl duration : duration_gleam = $'!timestamp |> timestamp.difference(!timestamp')'
            ($'duration.to_seconds(!duration)' : float)
            |> convert.convert
            |> fun x => x : int
            |> convert.convert
        | _ => fun () => $'!b - !a '

#!markdown

### total_seconds

#!spiral

inl total_seconds (time_span : time_span) : f64 =
    inl n : i64 =
        run_target function
            | Cuda => fun () => $'!time_span.total_seconds() * 10000000' : i64
            | Gleam => fun () =>
                backend_switch {
                    Gleam = fun () => global "import gleam/time/duration"
                }
                inl duration : duration_gleam = time_span |> $'duration.seconds'
                duration
                |> $'duration.to_seconds'
                |> (*) 10000000f64
                |> convert.convert
            | Lua => fun () =>
                inl seconds = time_span |> convert.unbox : f64
                seconds * 10000000 |> convert.convert : i64
            | Rust (Some (Contract)) => fun () => null ()
            | _ => fun () => time_span |> $'_.Ticks' : i64
    n / 10000000 |> convert.convert

#!markdown

### days

#!spiral

inl days (time_span : time_span) : f64 =
    backend_switch {
        Fsharp = fun () => time_span |> $'_.TotalDays' : f64
        Python = fun () => $'!time_span.days' : f64
        Gleam = fun () =>
            backend_switch {
                Gleam = fun () => global "import gleam/time/duration"
            }
            inl duration : duration_gleam = time_span |> $'duration.seconds'
            duration
            |> $'duration.to_seconds'
            |> (/) 86400f64
        Lua = fun () =>
            inl seconds = time_span |> convert.unbox : f64
            seconds / 86400 |> convert.convert : f64
    }

#!markdown

### ticks

#!spiral

inl ticks (date_time : date_time) : timestamp =
    backend_switch {
        Gleam = fun () =>
            date_time
            |> new_time_span (min_value ())
            |> total_seconds
            |> ((*) 10000000)
            |> convert.convert
            |> fun x => x : int
            |> timestamp
        Lua = fun () =>
            date_time
            |> new_time_span (min_value ())
            |> total_seconds
            |> ((-) 14400)
            |> ((*) -10000000)
            |> convert.convert
            : timestamp
        Fsharp = fun () =>
            date_time
            |> new_time_span (min_value ())
            |> total_seconds
            |> ((*) 10000000)
            |> convert.convert
            : timestamp
        Python = fun () =>
            date_time
            |> new_time_span (min_value ())
            |> total_seconds
            |> ((*) 10000000)
            |> convert.to
            : timestamp
    }

#!spiral

///- --test
///> gleam
///> lua
///> fsharp
///> cuda
///> rust

unix_epoch ()
|> ticks
|> _assert_eq' (621355968000000000i64 |> timestamp)

#!markdown

### hours

#!spiral

inl hours (time_span : time_span) : i32 =
    backend_switch {
        Gleam = fun () =>
            inl duration : duration_gleam = time_span |> $'duration.hours'
            $'duration.to_hours(!duration)' : i32
        Lua = fun () =>
            inl total_seconds = time_span |> convert.unbox : f64
            total_seconds / 3600.0 |> convert.convert : i32
        Fsharp = fun () => time_span |> $'_.Hours' : i32
        Python = fun () => $'!time_span.seconds // 3600' : i32
    }

#!spiral

///- --test
///> fsharp
///> cuda
///> rust
///> typescript
///> python

(join date_time_milliseconds 2002 2 2 20 22 24 26)
|> new_time_span (date_time_milliseconds 2001 1 1 10 11 12 13)
|> hours
|> _assert_eq 10

#!markdown

### minutes

#!spiral

inl minutes (time_span : time_span) : i32 =
    backend_switch {
        Fsharp = fun () => time_span |> $'_.Minutes' : i32
        Python = fun () => $'(!time_span.seconds // 60) % 60' : i32
    }

#!spiral

///- --test
///> fsharp
///> cuda
///> rust
///> typescript
///> python

(join date_time_milliseconds 2002 2 2 20 22 24 26)
|> new_time_span (date_time_milliseconds 2001 1 1 10 11 12 13)
|> minutes
|> _assert_eq 11

#!markdown

### seconds

#!spiral

inl seconds (time_span : time_span) : i32 =
    backend_switch {
        Fsharp = fun () => time_span |> $'_.Seconds' : i32
        Python = fun () => $'!time_span.seconds % 60' : i32
    }

#!spiral

///- --test
///> fsharp
///> cuda
///> rust
///> typescript
///> python

(join date_time_milliseconds 2002 2 2 20 22 24 26)
|> new_time_span (date_time_milliseconds 2001 1 1 10 11 12 13)
|> seconds
|> _assert_eq 12

#!markdown

### milliseconds

#!spiral

inl milliseconds (time_span : time_span) : i32 =
    backend_switch {
        Gleam = fun () =>
            inl duration : duration_gleam = time_span |> $'duration.milliseconds'
            $'duration.to_milliseconds(!duration)' : i32
        Lua = fun () =>
            inl total_seconds = time_span |> convert.unbox : f64
            (total_seconds * 1000.0) % 1000.0 |> convert.convert : i32
        Fsharp = fun () => time_span |> $'_.Milliseconds' : i32
        Python = fun () => $'!time_span.microseconds' : i32
    }

#!spiral

///- --test
///> fsharp
///> cuda
///> rust
///> typescript
///> python

(join date_time_milliseconds 2002 2 2 20 22 24 26)
|> new_time_span (date_time_milliseconds 2001 1 1 10 11 12 13)
|> milliseconds
|> _assert_eq 13

#!markdown

### time_span_format

#!spiral

inl time_span_format (format : string) (time_span : time_span) : string =
    run_target_args (fun () => format, time_span) function
        | TypeScript _
        | Python _ => fun format, time_span =>
            $'!time_span.ToString ("c", System.Globalization.CultureInfo.InvariantCulture)'
        | Rust _ => fun format, time_span =>
            open rust.rust_operators
            inl format = format
            !\\((time_span, format), $'"$0.toString($1)"')
        | _ => fun format, time_span =>
            backend_switch {
                Fsharp = fun () => $'!time_span.ToString !format ' : string
                Python = fun () =>
                    inl date = $'!time_span + !(min_value ())' : date_time
                    inl time = $'!date.strftime("%H:%M:%S")' : string
                    inl days = time_span |> days
                    inl result =
                        if days <= 0
                        then time
                        else #?**days ++# time
                    inl ms = time_span |> milliseconds
                    if ms <= 0
                    then result
                    else
                        inl ms =
                            #?**ms
                            |> sm'.pad_left 3 '0'
                            |> sm'.pad_right 7 '0'
                        result ++# ms
                Gleam = fun () =>
                    global "import tempo/datetime"
                    inl tempo_date_time : tempo_date_time =
                        time_span
                        |> $'duration.seconds'
                        |> fun x => x : duration_gleam
                        |> $'datetime.from_duration'
                    inl format =
                        if format = ""
                        then "c"
                        else format
                    $'!tempo_date_time |> datetime.format(tempo.Custom(!format))' : string
                Lua = fun () =>
                    inl total_seconds = time_span |> convert.unbox : f64
                    inl hours = total_seconds / 3600
                    inl minutes = (total_seconds % 3600) / 60
                    inl seconds = total_seconds % 60
                    inl result =
                        sm'.pad_left 2 '0' #?hours
                        ++# ":"
                        ++# sm'.pad_left 2 '0' #?minutes
                        ++# ":"
                        ++# sm'.pad_left 2 '0' #?seconds
                    inl ms = time_span |> milliseconds
                    if ms <= 0
                    then result
                    else
                        inl ms =
                            #?**ms
                            |> sm'.pad_left 3 '0'
                            |> sm'.pad_right 7 '0'
                        result ++# ms
            }

#!spiral

///- --test
///> fsharp
///> cuda
///> rust
///> typescript
///> python

(join date_time_milliseconds 2002 2 2 20 22 24 26)
|> new_time_span (date_time_milliseconds 2001 1 1 10 11 12 13)
|> time_span_format "c"
|> _assert_eq (backend_switch {
    Fsharp = fun () => "397.10:11:12.0130000"
    Python = fun () => "39710:11:120130000"
})

#!spiral

///- --test
///> fsharp
///> cuda
///> rust
///> typescript
///> python

(join date_time_milliseconds 2002 2 2 20 22 24 26)
|> new_time_span (date_time_milliseconds 2002 2 2 10 11 12 26)
|> time_span_format "c"
|> _assert_eq "10:11:12"

#!spiral

///- --test
///> fsharp
///> cuda
///> rust
///> typescript
///> python

(join date_time_milliseconds 2002 2 2 20 22 24 999)
|> new_time_span (date_time_milliseconds 2002 2 2 10 11 12 0)
|> time_span_format "c"
|> _assert_eq (backend_switch {
    Fsharp = fun () => "10:11:12.9990000"
    Python = fun () => "10:11:129990000"
})

#!markdown

### time_zone_info

#!spiral

nominal time_zone_info = $'System.TimeZoneInfo'

#!markdown

### add_days

#!spiral

inl add_days (days : i32) (date_time : date_time) : date_time =
    backend_switch {
        Fsharp = fun () =>
            $'!date_time.AddDays' days : date_time
        Python = fun () =>
            $'!date_time + datetime.timedelta(days=!days)' : date_time
        Gleam = fun () =>
            global "import gleam/time/duration"
            inl duration : duration_gleam = $'duration.days(#days)' : duration_gleam
            inl (date : date_gleam), (time_of_day : time_of_day_gleam), (existing_duration : duration_gleam) =
                date_time |> split_date_time
            inl new_duration : duration_gleam = $'!existing_duration + !duration '
            (date, time_of_day, new_duration) |> convert.to : date_time
        Lua = fun () =>
            inl seconds = days * 86400
            (date_time |> convert.unbox) + seconds |> convert.unbox : date_time
    }

#!markdown

### to_calendar

#!spiral

inl to_calendar (timestamp : timestamp) : date_time =
    backend_switch {
        Gleam = fun () =>
            global "import gleam/time/timestamp"
            global "import gleam/time/calendar"
    }
    inl duration = $'calendar.local_offset()' : duration_gleam
    inl (date : date_gleam), (time_of_day : time_of_day_gleam) = $'!timestamp |> timestamp.to_calendar(!duration)'
    (date, time_of_day, duration) |> convert.to

#!markdown

### now

#!spiral

inl now () : date_time =
    backend_switch {
        Gleam = fun () =>
            inl timestamp = $'timestamp.system_time ()' : timestamp
            timestamp |> to_calendar
        Fsharp = fun () =>
            run_target function
                | Rust (Some (Contract)) => fun () => null ()
                | _ => fun () => $'System.DateTime.Now'
            : date_time
        Python = fun () => $'datetime.datetime.now()' : date_time
    }

#!markdown

### utc_now

#!spiral

inl utc_now () : date_time =
    backend_switch {
        Fsharp = fun () => $'System.DateTime.UtcNow' : date_time
        Python = fun () => $'datetime.datetime.utcnow()' : date_time
    }

#!markdown

### stopwatch

#!spiral

nominal stopwatch_python =
    `(
        global "import timeit"
        $'' : $'timeit.default_timer'
    )
type stopwatch_switch =
    {
        Fsharp : $'System.Diagnostics.Stopwatch'
        Python : stopwatch_python
    }
nominal stopwatch = $'backend_switch `(stopwatch_switch)'

inl stopwatch () : stopwatch =
    backend_switch {
        Fsharp = fun () => $'`stopwatch ()' : stopwatch
        Python = fun () => $'`stopwatch ' : stopwatch
    }

#!spiral

inl stopwatch_elapsed_milliseconds (stopwatch : stopwatch) : i64 =
    $'!stopwatch.ElapsedMilliseconds'

#!spiral

inl stopwatch_start (stopwatch : stopwatch) : () =
    $'!stopwatch.Start' ()

#!markdown

## rust

#!markdown

### duration'

#!spiral

nominal duration' =
    `(
        backend_switch `(()) `({}) {
            Fsharp = (fun () => global "#if FABLE_COMPILER\n[<Fable.Core.Erase; Fable.Core.Emit(\"std::time::Duration\")>]\n#endif\ntype std_time_Duration = class end") : () -> ()
        }
        $'' : $'std_time_Duration'
    )

#!markdown

### date_time'

#!spiral

nominal date_time' t =
    `(
        backend_switch `(()) `({}) {
            Fsharp = (fun () => global "#if FABLE_COMPILER\n[<Fable.Core.Erase; Fable.Core.Emit(\"chrono::DateTime<$0>\")>]\n#endif\ntype chrono_DateTime<'T> = class end") : () -> ()
        }
        $'' : $'chrono_DateTime<`t>'
    )

#!markdown

### local

#!spiral

nominal local =
    `(
        global "#if FABLE_COMPILER\n[<Fable.Core.Erase; Fable.Core.Emit(\"chrono::Local\")>]\n#endif\ntype chrono_Local = class end"
        $'' : $'chrono_Local'
    )

#!markdown

### naive_date_time

#!spiral

nominal naive_date_time =
    `(
        backend_switch `(()) `({}) {
            Fsharp = (fun () => global "#if FABLE_COMPILER\n[<Fable.Core.Erase; Fable.Core.Emit(\"chrono::NaiveDateTime\")>]\n#endif\ntype chrono_NaiveDateTime = class end") : () -> ()
        }
        $'' : $'chrono_NaiveDateTime'
    )

#!markdown

## utc

#!spiral

nominal utc =
    `(
        global "#if FABLE_COMPILER\n[<Fable.Core.Erase; Fable.Core.Emit(\"chrono::Utc\")>]\n#endif\ntype chrono_Utc = class end"
        $'' : $'chrono_Utc'
    )

#!markdown

### naive_utc

#!spiral

inl naive_utc (date_time : date_time' utc) : naive_date_time =
    !\\(date_time, $'"$0.naive_utc()"')

#!markdown

### to_local

#!spiral

inl to_local (date_time : date_time' utc) : date_time' local =
    inl naive_date_time = date_time |> naive_utc
    !\\(naive_date_time, $'"chrono::offset::TimeZone::from_utc_datetime(&chrono::Local, &$0)"')

#!markdown

### from_timestamp_micros

#!spiral

inl from_timestamp_micros forall t {number; int}. (timestamp : t) : option (date_time' utc) =
    inl result : optionm'.option' (date_time' utc) =
        !\\(timestamp, $'"chrono::DateTime::from_timestamp_micros($0)"')
    result |> optionm'.unbox

#!markdown

### format'

#!spiral

inl format' (format : string) (date_time : date_time' utc) : sm'.std_string =
    !\\((date_time, #format), $'"$0.format($1).to_string()"')

#!markdown

### format''

#!spiral

inl format'' (format : string) (date_time : date_time' _) : sm'.std_string =
    !\\((date_time, #format), $'"$0.format($1).to_string()"')

#!markdown

### format_timestamp

#!spiral

inl format_timestamp forall t {number; int}. (timestamp : t) =
    inl timestamp = join timestamp
    (timestamp / 1000)
    |> from_timestamp_micros
    |> optionm.map fun x =>
        x
        |> to_local
        |> format'' "%Y-%m-%d %H:%M:%S"
        |> sm'.from_std_string
    |> resultm.from_option

#!markdown

### duration_from_millis

#!spiral

inl duration_from_millis (ms : u64) : duration' =
    inl ms = join ms
    !\($'"std::time::Duration::from_millis(!ms)"')

#!markdown

## date_time

#!markdown

### time_zone_local

#!spiral

inl time_zone_local () : time_zone_info =
    run_target function
        | Fsharp _ => fun () =>
            $'System.TimeZoneInfo.Local'
        | Rust (None) => fun () =>
            open rust.rust_operators
            !\($'"std::sync::Arc::new(chrono::FixedOffset::local_minus_utc(chrono::Local::now().offset()) as i64)"')
        | _ => fun () => null ()

#!markdown

### get_utc_offset

#!spiral

inl get_utc_offset (time_zone_info : time_zone_info) (date_time : date_time) : time_span =
    run_target function
        | Fsharp _ => fun () => date_time |> $'_.GetUtcOffset' (time_zone_local ())
        | Rust (None | Some (Wasm)) => fun () =>
            open rust.rust_operators
            inl ticks = date_time |> ticks
            // inl ticks = ticks |> rust.rust.emit
            (!\\((date_time, ticks), $'"chrono::FixedOffset::local_minus_utc(&chrono::DateTime::timezone(&chrono::DateTime::fixed_offset(&chrono::DateTime::from_timestamp_nanos($1))))"') : i32)
            |> fun x => $'!x |> `time_span '
        | target => fun () =>
            backend_switch {
                Fsharp = fun () => failwith $'$"date_time.get_utc_offset / target: {!target}"' : time_span
                Python = fun () => $'!date_time.utcoffset()' : time_span
                Gleam = fun () =>
                    global "import gleam/time/duration"
                    inl duration : duration_gleam = $'calendar.utc_offset()' : duration_gleam
                    duration
                    |> $'duration.to_seconds'
                    |> ((*) 10000000i32)
                    |> convert.convert
                    |> fun x => x : int
                    |> convert.unbox
                    : time_span
                Lua = fun () =>
                    inl date_time = date_time |> convert.unbox : int
                    inl offset = 5i32 * 3600i32
                    offset * 10000000 |> convert.convert : time_span
            }

#!markdown

### date_time_guid_from_date_time

#!spiral

let date_time_guid_from_date_time (guid' : guid.guid) (date_time : date_time) =
    inl create prefix time_zone : date_time_guid =
        inl guid_range =
            guid'
            |> sm'.obj_to_string
            |> sm'.range
                (am'.Start (##prefix + ##time_zone))
                (am'.End eval)
        prefix ++# time_zone ++# guid_range |> guid.new_guid
    run_target function
        | Rust (Some (Contract)) => fun () => null ()
        | Rust (None | Some (Wasm)) => fun () =>
            inl epoch =
                unix_epoch ()
                |> to_universal_time
            inl date_time =
                date_time
                |> specify_date_kind Local
                |> to_universal_time
            inl unixticks =
                match date_time |> ticks, epoch |> ticks with
                | timestamp date_time, timestamp epoch =>
                    convert.convert date_time - convert.convert epoch : i64
            inl prefix =
                unixticks / 10
                |> from_timestamp_micros
                |> optionm.map (
                    to_local
                    >> format'' "%Y%m%d-%H%M-%S%f"
                    >> sm'.from_std_string
                    >> fun s => $'$"{!s.[0..17]}-{!s.[18..21]}-{!s.[22]}"'
                )
                |> optionm'.default_value ""
            inl time_zone = date_time |> get_utc_offset (time_zone_local ())
            inl time_zone_signal = if hours time_zone > 0 then 1u8 else 0
            inl time_zone_value = time_zone |> time_span_format (join "hh:mm")
            inl time_zone = $'$"{!time_zone_signal}{!time_zone_value.[0..1]}{!time_zone_value.[3..4]}"'
            create prefix time_zone
        | target => fun () =>
            inl prefix = date_time |> format (join "yyyyMMdd-HHmm-ssff-ffff-f")
            inl time_zone = date_time |> get_utc_offset (time_zone_local ())
            inl time_zone_signal = if hours time_zone > 0 then 1u8 else 0
            inl time_zone_value = time_zone |> time_span_format (join "hhmm")
            inl time_zone = #?time_zone_signal ++# time_zone_value
            create prefix time_zone

#!spiral

///- --test
///> fsharp
////> cuda
////> rust -d chrono // Ambiguous local time, ranging from 1970-01-01T00:00:00-03:00 to 1970-01-01T00:00:00-04:00

inl suffix = test_guid () |> sm'.obj_to_string |> sm'.range (am'.End fun x => x () - 6i32) (am'.End eval)
unix_epoch ()
|> specify_date_kind Utc
|> to_universal_time
|> date_time_guid_from_date_time (test_guid ())
|> sm'.obj_to_string
|> fun s => s |> _assert_eq' ((s |> sm'.slice 0i32 29) ++# suffix)

#!spiral

///- --test
///> fsharp
///> rust -d chrono

inl suffix = test_guid () |> sm'.obj_to_string |> sm'.range (am'.End fun x => x () - 6i32) (am'.End eval)
min_value ()
|> specify_date_kind Local
|> date_time_guid_from_date_time (test_guid ())
|> sm'.obj_to_string
|> fun s => s |> _assert_eq' $'$"00010101-0000-0000-0000-0{!(s |> sm'.slice 25i32 29)}{!suffix}"'

#!spiral

///- --test
///> fsharp

inl suffix = test_guid () |> sm'.obj_to_string |> sm'.range (am'.End fun x => x () - 6i32) (am'.End eval)
max_value ()
|> specify_date_kind Utc
|> add_days -1
|> date_time_guid_from_date_time (test_guid ())
|> sm'.obj_to_string
|> fun s => s |> _assert_eq ("99991230-2359-5999-9999-9" ++# (s |> sm'.slice 25i32 29) ++# suffix)

#!spiral

///- --test
///> rust -d chrono

inl suffix = test_guid () |> sm'.obj_to_string |> sm'.range (am'.End fun x => x () - 6i32) (am'.End eval)
max_value ()
|> specify_date_kind Utc
|> add_days -1
|> date_time_guid_from_date_time (test_guid ())
|> sm'.obj_to_string
|> fun s => s |> _assert_eq ("99991230-2359-5900-0012-0" ++# (s |> sm'.slice 25i32 29) ++# suffix)

#!spiral

///- --test
///> fsharp
///> rust -d chrono

inl suffix = test_guid () |> sm'.obj_to_string |> sm'.range (am'.End fun x => x () - 6i32) (am'.End eval)
unix_epoch ()
|> specify_date_kind Utc
|> add_days 1
|> date_time_guid_from_date_time (test_guid ())
|> sm'.obj_to_string
|> fun s => s |> _assert_eq ("19700102-0000-0000-0000-0" ++# (s |> sm'.slice 25i32 29) ++# suffix)

#!markdown

### date_time_from_guid

#!spiral

inl date_time_from_guid (date_time_guid : date_time_guid) =
    inl date_time_guid = date_time_guid |> sm'.obj_to_string
    inl sm_replace = sm'.replace "-" ""
    run_target_args (fun () => sm_replace) function
        | (Rust _ | TypeScript _) => fun sm_replace =>
            $'System.DateTime.Parse (!date_time_guid.[..24] |> !sm_replace)' : date_time
        | _ => fun sm_replace => $'System.DateTime.ParseExact (!date_time_guid.[..24] |> !sm_replace, "yyyyMMddHHmmssfffffff", null)' : date_time

#!spiral

///- --test

date_time_from_guid (guid.new_guid "00010101-0000-0000-0000-0a9876543210")
|> _assert_eq' (min_value ())

#!spiral

///- --test

date_time_from_guid (guid.new_guid $'$"99991231-2359-5999-9999-9{(!test_guid () |> string).[^10..]}"')
|> _assert_eq' (max_value ())

#!spiral

///- --test

date_time_from_guid (guid.new_guid $'$"19700101-0000-0000-0000-0{(!test_guid () |> string).[^10..]}"')
|> _assert_eq' $'System.DateTime.UnixEpoch'

#!markdown

### timestamp_guid_from_timestamp

#!spiral

inl timestamp_guid_from_timestamp (guid : guid.guid) (timestamp : timestamp) : timestamp_guid =
    inl guid = guid |> sm'.obj_to_string
    inl timestamp = timestamp |> sm'.obj_to_string |> sm'.pad_left 18i32 '0'
    $'`timestamp_guid $"{!timestamp.[0..7]}-{!timestamp.[8..11]}-{!timestamp.[12..15]}-{!timestamp.[16..17]}{!guid.[21..]}"'

#!spiral

///- --test

timestamp_guid_from_timestamp (test_guid ()) (0i64 |> timestamp)
|> _assert_eq' (guid.new_guid "00000000-0000-0000-0043-210fedcba987")

#!spiral

///- --test

timestamp_guid_from_timestamp (test_guid ()) (999999999999999999i64 |> timestamp)
|> _assert_eq' (guid.new_guid $'$"99999999-9999-9999-9943-2{(!test_guid () |> string).[^10..]}"')

#!markdown

### timestamp_from_guid

#!spiral

inl timestamp_from_guid (guid : date_time_guid) : timestamp =
    inl guid = guid |> sm'.obj_to_string
    $'`i64 $"{!guid.[0..7]}{!guid.[9..12]}{!guid.[14..17]}{!guid.[19..20]}"'

#!spiral

///- --test

timestamp_from_guid (guid.new_guid "00000000-0000-0000-00dc-ba9876543210")
|> _assert_eq' (0i64 |> timestamp)

#!spiral

///- --test

timestamp_from_guid (guid.new_guid $'$"99999999-9999-9999-99{(!test_guid () |> string).[^14..]}"')
|> _assert_eq' (999999999999999999i64 |> timestamp)

#!markdown

### new_guid_from_date_time

#!spiral

inl new_guid_from_date_time (date_time : date_time) =
    inl guid = guid.new_raw_guid ()
    date_time |> date_time_guid_from_date_time guid

#!spiral

///- --test

utc_now ()
|> new_guid_from_date_time
|> date_time_from_guid
|> fun date_time => new_time_span date_time (utc_now ()) |> total_seconds |> convert.i32
|> _assert_eq 0

#!markdown

### new_guid_from_timestamp

#!spiral

inl new_guid_from_timestamp (timestamp : timestamp) =
    inl guid = guid.new_raw_guid ()
    timestamp |> timestamp_guid_from_timestamp guid

#!spiral

///- --test

utc_now ()
|> ticks
|> new_guid_from_timestamp
|> timestamp_from_guid
|> fun (timestamp t) =>
    (convert.convert t - (utc_now () |> ticks |> fun (timestamp x) => convert.convert x)) / 100000i64
|> _assert_eq 0i64

#!markdown

## main

#!spiral

inl main () =
    $'let date_time_guid_from_date_time x = !date_time_guid_from_date_time x' : ()
    $'let date_time_from_guid x = !date_time_from_guid x' : ()
    $'let timestamp_guid_from_timestamp x = !timestamp_guid_from_timestamp x' : ()
    $'let timestamp_from_guid x = !timestamp_from_guid x' : ()
    $'let new_guid_from_date_time x = !new_guid_from_date_time x' : ()
    $'let new_guid_from_timestamp x = !new_guid_from_timestamp x' : ()
    $'let format x = !format x' : ()
    $'let format_iso8601 x = !format_iso8601 x' : ()
