#!meta

{"kernelInfo":{"defaultKernelName":"spiral","items":[{"name":"spiral"}]}}

#!markdown

# math

#!spiral

///- --test static

open testing

#!markdown

## math

#!markdown

### e

#!spiral

inl e () =
    exp 1f64

#!markdown

## square

#!spiral

inl square x =
    x ** 2

#!spiral

///- --test

5f64
|> sqrt
|> square
|> _assert_approx_eq None 5

#!spiral

///- --test

e () |> square
|> _assert_approx_eq None 7.3890560989306495

#!spiral

///- --test

2 * 2 / 0.4f64 |> sqrt
|> _assert_approx_eq None 3.1622776601683795

#!spiral

///- --test

2f64 / 3
|> _assert_approx_eq None 0.6666666666666666

#!spiral

///- --test

2f64 |> log
|> _assert_approx_eq None 0.6931471805599453

#!spiral

///- --test

pi
|> _assert_approx_eq None 3.141592653589793f64

#!spiral

///- --test

pi |> cos
|> _assert_eq -1f64

#!spiral

///- --test

pi
|> cos
|> fun n => n / 2f64
|> _assert_approx_eq None -0.5

#!spiral

///- --test

pi / 2 |> cos
|> _assert_approx_eq None 0.00000000000000006123233995736766f64

#!markdown

## fsharp

#!markdown

### atan2

#!spiral

inl atan2 (y : f64) (x : f64) : f64 =
    backend_switch {
        Lua = fun () => $'math.atan2(!y, !x)' : float
        Fsharp = fun () => $'System.Math.Atan2 (!y, !x)' : float
    }

#!spiral

///- --test
///> lua
///> fsharp

0 |> atan2 1
|> _assert_eq 1.5707963267948966

#!markdown

## floor

#!spiral

inl floor forall t {float}. (n : t) : t =
    backend_switch {
        Lua = fun () => n |> $'math.floor' : t
        Fsharp = fun () => n |> $'floor' : t
    }

#!spiral

///- --test

0.6 |> floor
|> _assert_eq 0f64

#!markdown

## ceil

#!spiral

inl ceil forall t {float}. (n : t) : t =
    backend_switch {
        Lua = fun () => n |> $'math.ceil' : t
        Fsharp = fun () => n |> $'ceil' : t
    }

#!spiral

///- --test
///> lua
///> fsharp

0.6 |> ceil
|> _assert_eq 1f64

#!markdown

## round

#!spiral

inl round forall t {number; float}. (x : t) : t =
    backend_switch {
        Lua = fun () =>
            if x < 1
            then x - 0.5 |> $'math.ceil' : t
            else
                inl result = x + 1 |> $'math.floor'
                if result % 2 = 0
                then result
                else result - 1
        Fsharp = fun () => x |> $'round' : t
    }

#!spiral

///- --test
///> lua
///> fsharp

0.5 |> round
|> _assert_eq 0f64

0.6 |> round
|> _assert_eq 1f64

1.5 |> round
|> _assert_eq 2f64

2.5 |> round
|> _assert_eq 2f64

3.5 |> round
|> _assert_eq 4f64

#!markdown

## log_base

#!spiral

inl log_base (new_base : f64) (a : f64) : f64 =
    $'System.Math.Log (!a, !new_base)'

#!spiral

///- --test

100 |> log_base 10
|> _assert_eq 2
