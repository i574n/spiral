/// # math

/// ## math

/// ### e
inl e () =
    exp 1f64

/// ## square
inl square x =
    x ** 2

/// ## fsharp

/// ### atan2
inl atan2 (y : f64) (x : f64) : f64 =
    backend_switch {
        Gleam = fun () =>
            global "import gleam_community/maths"
            $'maths.atan2(!y, !x)' : float
        Lua = fun () => $'math.atan2(!y, !x)' : float
        Fsharp = fun () => $'System.Math.Atan2 (!y, !x)' : float
        Python = fun () => 
            global "import math"
            $'math.atan2(!y, !x)' : float
    }

/// ## floor
inl floor forall t {float}. (n : t) : t =
    backend_switch {
        Gleam = fun () =>
            global "import gleam/float"
            n |> $'float.floor' : t
        Lua = fun () => n |> $'math.floor' : t
        Fsharp = fun () => n |> $'floor' : t
        Python = fun () => 
            global "import math"
            $'math.floor(!n)' : t
    }

/// ## ceil
inl ceil forall t {float}. (n : t) : t =
    backend_switch {
        Gleam = fun () =>
            global "import gleam/float"
            n |> $'float.ceiling' : t
        Lua = fun () => n |> $'math.ceil' : t
        Fsharp = fun () => n |> $'ceil' : t
        Python = fun () =>
            global "import math"
            n |> $'math.ceil' : t
    }

/// ## round
inl round forall t {number; float}. (x : t) : t =
    backend_switch {
        Gleam = fun () =>
            if x < 1
            then x - 0.5 |> ceil
            else
                inl result = x + 1 |> floor |> convert.i32
                if result % 2 = 0
                then result
                else result - 1
                |> convert.convert
        Lua = fun () =>
            if x < 1
            then x - 0.5 |> ceil
            else
                inl result = x + 1 |> floor
                if result % 2 = 0
                then result
                else result - 1
        Fsharp = fun () => x |> $'round' : t
        Python = fun () => x |> $'round' : t
    }

/// ## log_base
inl log_base (new_base : f64) (a : f64) : f64 =
    $'System.Math.Log (!a, !new_base)'
