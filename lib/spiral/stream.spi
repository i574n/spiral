/// # stream
open rust.rust_operators

/// ## stream

/// ### stream
union rec stream t =
    | StreamCons : t * (() -> stream t)
    | StreamNil

/// ### fold
inl fold fn init s =
    inl rec body acc = function
        | StreamCons (st, fn') => 루프 (fn acc st) (fn' ())
        | StreamNil => acc
    and inl 루프 acc = join_body body acc
    루프 init s

/// ### fold_back
inl fold_back fn s init =
    inl rec body acc = function
        | StreamCons (st, fn') => fn st (루프 acc (fn' ()))
        | StreamNil => acc
    and inl 루프 acc = join_body body acc
    루프 init s

/// ### to_list
inl to_list s =
    (s, [])
    ||> fold_back fun x acc =>
        x :: acc

/// ### rev
inl rev s =
    (StreamNil, s)
    ||> fold fun s x =>
        StreamCons (x, fun () => s)

/// ### from_list
inl from_list list =
    (list, StreamNil)
    ||> listm.foldBack fun x acc =>
        StreamCons (x, fun () => acc)

/// ### try_item
inl try_item i s =
    // inl rec body i = function
    inl rec body forall a {number} b. (i : a) : stream b -> option b = function
        | StreamCons (x, _) when i <= 0 => Some x
        | StreamCons (_, fn) => 루프 (i - 1) (fn ())
        | StreamNil => None
    // and inl 루프 acc s' =
    and inl 루프 forall a {number} b. (acc : a) (s' : stream b) : option b =
        match var_is acc, var_is s' with
        | false, false => body acc s'
        | _ =>
            inl acc = dyn acc
            inl s' = dyn s'
            join body acc s'
    루프 i s

inl item i =
    try_item i >> optionm.value

/// ### new_infinite_stream
inl new_infinite_stream fn =
    inl rec 루프 n =
        StreamCons (fn n, fun () => 루프 (n + 1))
    루프 0

inl new_infinite_stream_ fn =
    let rec 루프 n =
        StreamCons (fn n, fun () => 루프 (n + 1))
    루프 0

/// ### new_finite_stream
inl new_finite_stream fn max =
    inl rec 루프 n =
        if n >= max
        then StreamNil
        else StreamCons (fn n, fun () => 루프 (n + 1))
    루프 0

/// ### memoize
union memoized_stream t =
    | NotComputed : () -> stream t
    | Computed : stream t

inl memoize s =
    // inl rec body s =
    inl rec body forall t. (s : () -> stream t) : () -> stream t =
        inl state = mut (NotComputed s)
        fun () =>
            match *state with
            | Computed x => x
            | NotComputed fn =>
                inl new_state =
                    match fn () with
                    | StreamNil => StreamNil
                    | StreamCons (x, fn) => StreamCons (x, 루프 fn)
                state <- Computed new_state
                new_state
    // and inl 루프 s' = join_body_unit body s s'
    and inl 루프 forall t. (s' : () -> stream t) : () -> stream t =
        join_body_unit body s s'
    루프 (fun () => s)

/// ### unfold
inl unfold f x0 =
    inl rec body x =
        match f x with
        | Some (x', y) => StreamCons (x', fun () => 루프 y)
        | None => StreamNil
    and inl 루프 x = join_body_unit body x0 x
    루프 x0

/// ### iterate
inl iterate f =
    fun x => Some (x, f x)
    |> unfold

/// ### iterate'
inl iterate_map f m =
    fun x =>
        m x
        |> optionm.map fun x =>
            x, f x
    |> unfold

/// ### take_while
inl take_while cond s =
    inl rec body i = function
        | StreamCons (st, fn) when cond st i => StreamCons (st, fun () => 루프 (i + 1) (fn ()))
        | _ => StreamNil
    and inl 루프 i = join_body body i
    루프 0 s

/// ### sum
inl sum seq =
    seq |> fold (+) 0

/// ### indexed
inl indexed s =
    ((StreamNil, 0), s)
    ||> fold fun (acc, i) x =>
        StreamCons ((i, x), fun () => acc), i + 1
    |> fst
    |> rev

/// ### map
inl map fn s =
    (s, StreamNil)
    ||> fold_back fun x acc =>
        StreamCons (fn x, fun () => acc)

/// ### zip_with
inl zip_with fn s1 s2 =
    inl rec 루프 s1 s2 =
        match s1, s2 with
        | StreamCons (st1, fn1), StreamCons (st2, fn2) =>
            StreamCons (fn st1 st2, fun () => 루프 (fn1 ()) (fn2 ()))
        | StreamNil, _ | _, StreamNil => StreamNil
    루프 s1 s2

inl zip_with_ fn s1 s2 =
    let rec 루프 s1 s2 =
        match s1, s2 with
        | StreamCons (st1, fn1), StreamCons (st2, fn2) =>
            StreamCons (fn st1 st2, fun () => 루프 (fn1 ()) (fn2 ()))
        | StreamNil, _ | _, StreamNil => StreamNil
    루프 s1 s2

/// ### zip
inl zip s1 s2 =
    zip_with pair s1 s2

inl zip_ s1 s2 =
    zip_with_ pair s1 s2

/// ### unzip
inl unzip s =
    // inl rec body s =
    inl rec body forall a b. (s : stream (a * b)) : stream a * stream b =
        match s with
        | StreamCons ((x, y), fn) =>
            inl xs, ys = 루프 (fn ())
            StreamCons (x, fun () => xs), StreamCons (y, fun () => ys)
        | StreamNil => pair StreamNil StreamNil
    // and inl 루프 x =
    and inl 루프 forall a b. (x : stream (a * b)) : stream a * stream b =
        if var_is x |> not
        then body x
        else
            inl x = dyn x
            join body x
    루프 s

/// ## rust

/// ### io_error
nominal io_error =
    `(
        global "#if FABLE_COMPILER\n[<Fable.Core.Erase; Fable.Core.Emit(\"std::io::Error\")>]\ntype std_io_Error = class end\n\#else\ntype std_io_Error = string\n#endif\n"
        $'' : $'std_io_Error'
    )

/// ### new_io_error
inl new_io_error (text : string) : io_error =
    run_target_args (fun () => text) function
        | Rust _ => fun text =>
            !\\(text, $'"std::io::Error::new(std::io::ErrorKind::Other, &*$0)"')
        | _ => fun text => text |> unbox

/// ### buf_reader
nominal buf_reader t =
    `(
        backend_switch `(()) `({}) {
            Fsharp =
                (fun () =>
                    global "#if FABLE_COMPILER\n[<Fable.Core.Erase; Fable.Core.Emit(\"std::io::BufReader<$0>\")>]\n#endif\ntype std_io_BufReader<'T> = class end"
                ) : () -> ()
        }
        $'' : $'std_io_BufReader<`t>'
    )

/// ### cursor
nominal cursor t =
    `(
        backend_switch `(()) `({}) {
            Fsharp =
                (fun () =>
                    global "#if FABLE_COMPILER\n[<Fable.Core.Erase; Fable.Core.Emit(\"std::io::Cursor<$0>\")>]\n#endif\ntype std_io_Cursor<'T> = class end"
                ) : () -> ()
        }
        $'' : $'std_io_Cursor<`t>'
    )

/// ### buf_reader_tokio
nominal buf_reader_tokio t =
    `(
        global "#if FABLE_COMPILER\n[<Fable.Core.Erase; Fable.Core.Emit(\"tokio::io::BufReader<$0>\")>]\n#endif\ntype tokio_io_BufReader<'T> = class end"
        $'' : $'tokio_io_BufReader<`t>'
    )

/// ### new_buf_reader
inl new_buf_reader forall t. (x : t) : buf_reader t =
    !\\(x, $'"std::io::BufReader::new($0)"')

/// ### new_cursor
inl new_cursor forall t. (x : t) : cursor t =
    !\($'"std::io::Cursor::new(!x)"')

/// ### lines
nominal lines t =
    `(
        global "#if FABLE_COMPILER\n[<Fable.Core.Erase; Fable.Core.Emit(\"std::io::Lines<$0>\")>]\n#endif\ntype std_io_Lines<'T> = class end"
        $'' : $'std_io_Lines<`t>'
    )

/// ### buf_read_lines
inl buf_read_lines forall t. (buf_reader : buf_reader t) : lines (buf_reader t) =
    !\($'"std::io::BufRead::lines(!buf_reader)"')

/// ### decode_reader_bytes
nominal decode_reader_bytes t u =
    `(
        global "#if FABLE_COMPILER\n[<Fable.Core.Erase; Fable.Core.Emit(\"encoding_rs_io::DecodeReaderBytes<$0, $1>\")>]\n#endif\ntype encoding_rs_io_DecodeReaderBytes<'T, 'U> = class end"
        $'' : $'encoding_rs_io_DecodeReaderBytes<`t, `u>'
    )

/// ### decode_reader_bytes_build
inl decode_reader_bytes_build forall t. (x : t) : decode_reader_bytes t (am'.vec u8) =
    // !\($'"encoding_rs_io::DecodeReaderBytesBuilder::new().encoding(Some(encoding_rs::UTF_8)).build(!x)"')
    // !\($'"encoding_rs_io::DecodeReaderBytesBuilder::new().encoding(Some(encoding_rs::UTF_8)).utf8_passthru(true).build(!x)"')
    !\\(x, $'"encoding_rs_io::DecodeReaderBytesBuilder::new().utf8_passthru(true).build($0)"')
    // !\($'"encoding_rs_io::DecodeReaderBytes::new(!x)"')

/// ### buf_reader_read
inl buf_reader_read forall el dim. (slice : am'.slice' el dim) (buf_reader : buf_reader el) : resultm.result' unativeint io_error =
    (!\($'"true; let mut !slice = !slice"') : bool) |> ignore
    !\($'"std::io::Read::read(&mut !buf_reader, &mut !slice)"')

/// ### io_read_by_ref
inl io_read_by_ref forall t. (lines : lines t) : lines t =
    !\\(lines, $'"std::io::Read::by_ref($0)"')
